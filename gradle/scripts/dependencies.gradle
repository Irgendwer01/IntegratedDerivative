apply from: 'gradle/scripts/helpers.gradle'

def debug_terminals = true
def debug_tunnels = true
def debug_crafting = true

def run_spark = true
def run_drawers = true
def debug_forestry = true

final def mod_dependencies = [
        'integrated-dynamics-236307:3159505': [true],
        'common-capabilities-247007:3053017': [true],
        'cyclops-core-232758:3159497': [true],
        'jei-238222:5101347': [true],
        'integrated-terminals-295910:3010622': [debug_terminals],
        'integrated-tunnels-251389:3159511': [debug_tunnels],
        'integrated-crafting-287357:3010631': [debug_crafting],
        'forestry-59751:2684780': [debug_forestry],
]

final def runtime_dependencies = [
        'hwyla-253449:2568751': [true],
        'storage-drawers-223852:4655859': [run_drawers],
        'chameleon-230497:2450900': [run_drawers],
        // NOTE: don't run versions above 1.11.0 because they rely on a mixin that doesn't work in dev environment
        'spark-unforged-900927:4691415': [run_spark],
]

repositories {
    // Other repositories described by default:
    // CleanroomMC: https://maven.cleanroommc.com
    exclusiveContent {
        forRepository {
            maven {
                name 'CurseMaven'
                url 'https://cursemaven.com'
            }
        }
        filter {
            includeGroup 'curse.maven'
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name 'Modrinth'
                url 'https://api.modrinth.com/maven'
            }
        }
        filter {
            includeGroup 'maven.modrinth'
        }
    }
    mavenLocal() // Must be last for caching to work
}

dependencies {
    // Example - Dependency descriptor:
    // 'com.google.code.gson:gson:2.8.6' << group: com.google.code.gson, name:gson, version:2.8.6
    // 'group:name:version:classifier' where classifier is optional

    // Example - Deobfuscating dependencies:
    // rfg.deobf('curse.maven:had-enough-items-557549:4543375')
    // By wrapping a dependency descriptor in rfg.deobf() method call, the dependency is queued for deobfuscation
    // When deobfuscating, RFG respects the mapping_channel + mapping_version stated in gradle.properties

    // Example - CurseMaven dependencies:
    // 'curse.maven:had-enough-items-557549:4543375' << had-enough-items = project slug, 557549 = project id, 4543375 = file id
    // Full documentation: https://cursemaven.com/

    // Example - Modrinth dependencies:
    // 'maven.modrinth:jei:4.16.1.1000' << jei = project name, 4.16.1.1000 = file version
    // Full documentation: https://docs.modrinth.com/docs/tutorials/maven/

    // Common dependency types (configuration):
    // implementation = dependency available at both compile time and runtime
    // runtimeOnly = runtime dependency
    // compileOnly = compile time dependency
    // annotationProcessor = annotation processing dependencies

    // Transitive dependencies:
    // (Dependencies that your dependency depends on)
    // If you wish to exclude transitive dependencies in the described dependencies
    // Use a closure as such:
    // implementation ('com.google.code.gson:gson:2.8.6') {
    //     transitive = false
    // }

    mod_dependencies.entrySet().forEach {
        def mod = rfg.deobf('curse.maven:' + it.key)
        compileOnly mod
        if (it.value.any { it.toBoolean() }) {
            runtimeOnly mod
        }
    }
    runtime_dependencies.entrySet().forEach {
        if (it.value.any { it.toBoolean() }) {
            def mod = rfg.deobf('curse.maven:' + it.key)
            runtimeOnly mod
        }
    }
}